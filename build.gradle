plugins {
    id "java"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

defaultTasks "build"

group "eu.decentsoftware.holograms"
version "2.8.10"
description "A lightweight yet very powerful hologram plugin with many features and configuration options."

repositories {
    mavenCentral()
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://repo.codemc.io/repository/nms/" }
    maven { url = "https://repo.codemc.io/repository/maven-public/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    compileOnly "org.spigotmc:spigot:1.13-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.6"
    compileOnly "com.arcaniax:HeadDatabase-API:1.3.2"
    implementation "org.bstats:bstats-bukkit:3.0.2"
    implementation "de.tr7zw:item-nbt-api:2.13.1"
    implementation "com.github.cryptomorin:XSeries:10.0.0"

    // Annotations
    compileOnly "org.projectlombok:lombok:1.18.34"
    annotationProcessor "org.projectlombok:lombok:1.18.34"
    compileOnly "org.jetbrains:annotations:24.1.0"
    annotationProcessor "org.jetbrains:annotations:24.1.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = "UTF-8"
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
            artifactId "decentholograms"
        }
    }
}

processResources {
    def props = [version: version, description: project.description]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}

shadowJar {
    relocate "org.bstats", "eu.decentsoftware.holograms.metrics"
    relocate "de.tr7zw.changeme.nbtapi", "eu.decentsoftware.holograms.libs.nbtapi"
    relocate "com.cryptomorin.xseries", "eu.decentsoftware.holograms.libs.xseries"
}